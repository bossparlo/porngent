# univer
if $ARGS[0] = 'start':
	$sexloc = $CURLOC
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">The University of</font></b></center>'
	'<center><img src="images/etogame/univer.jpg"></center>'

	if student = 0 and abiturient = 0:'Preparatory courses cost 15,000'

	'Examinations for admission to the University are held in August.'
	'All lectures begin with a 8:00, late for a lecture is not allowed.'

	if student > 0:'You learn how to <<semestr>> semester <<kurs>> course'

	if abiturient = 1:'You entrant.'

	act 'Leave':
		cla
		minut += 5
		gt 'down'
	end

	if student = 0 and abiturient = 0 and money >= 15000 and month < 9 and diplom = 0 and age >= 17:
		''

		act 'Pay for training course (15,000 rubles)':
			cla
			minut += 5
			money -= 15000
			abiturient = 1
			'You paid 15,000 for preparatory courses. Classes start in 21chas, on weekdays'

			act 'Move away':gt 'univer', 'start'
		end
	elseif abiturient >= 1 and month < 9 and hour < 21 and week < 6:
		act 'Attend preparatory courses (1:00)':
			cla
			*clr
			minut += 60
			abiturient += 1
			intel += 1
			'<center><img src="images/etogame/Classroom.jpg"></center>'
			'You were engaged in preparatory courses within an hour.'

			act 'Get out of class':gt 'univer', 'start'
		end
	end

	if month = 8 and student = 0 and diplom = 0 and age >= 17:
		'You can try to take an entrance exam.'

		act 'Take the entrance exam (1:00)':
			cla
			*clr
			minut += 60
			'<center><img src="images/etogame/Classroom.jpg"></center>'
			'You passed the entrance exam to university.'

			if intel + abiturient >= 100:
				student = 1
				kurs = 1
				semestr = 1
				abiturient = 0
				'You have successfully passed the entrance exams and can start training in September.'
			else
				'You failed the entrance exam.'
			end

			act 'Get out of class':gt 'univer', 'start'
		end
	end

	if week < 6 and student > 0 and month > 8 and month < 12:
		!! 1st semester
		act 'Go to the deanery':
			cla

			if semestr = 2 and examen = 0:examen = 2

			gt 'univer', 'dekanat'
		end
	elseif week < 6 and student > 0 and month > 1 and month < 5:
		!! 2nd semester
		act 'Go to the deanery':
			cla

			if semestr = 1 and examen = 0:examen = 2

			gt 'univer', 'dekanat'
		end
	elseif week < 6 and student > 0 and month = 12 and examen = 0 and semestr = 1:
		!! 1st semester exams
		act 'Go for exams':gt 'univer', 'examen'
	elseif week < 6 and student > 0 and month = 5 and examen = 0 and semestr = 2:
		!! 2nd semester exams
		act 'Go for exams':gt 'univer', 'examen'
	end
end

if $ARGS[0] = 'dekanat':
	$sexloc = $CURLOC
	cla
	clr
	*clr
	minut += 5

	if examen = 1:
		examen = 0

		if semestr = 2:
			semestr = 1
			kurs += 1
		elseif semestr = 1:
			semestr = 2
		end
	elseif examen = 2:
		student = 0
		kurs = 0
		semestr = 0
		abiturient = 0
		stipuha = 0
		lektor = 0

		'YOU expelled from university.'
	end

	if kurs = 2:
		diplom = 1
		student = 0
		kurs = 0
		semestr = 0
		abiturient = 0
		stipuha = 0
		'YOU awarded a diploma.'
	end

	gs 'stat'
	'<center><b><font color="maroon">Dean</font></b></center>'
	'<center><img src="images/etogame/dekanat.jpg"></center>'
	'You learn how to <<semestr>> semester <<kurs>> course'
	'Prior to the session must pass <<kurs>> coursework. More welcomed and encouraged during the session.'

	if kursovikD > 0:'You passed <<kursovikD>> coursework.'

	act 'Escape from the dean''s office':gt 'univer', 'start'

	if hour < 8 and student > 0:
		act 'At the lecture':
			cla
			*clr
			minut += 360
			hour = 14
			minut = 0
			lektor += 1
			intel += 1
			'<center><img src="images/etogame/Classroom1.jpg"></center>'
			'You were sitting in lectures and recorded for teacher notes.'
			zanrand = rand(0, 9)

			if zanrand = 0:
				minut += 60
				'During class lecturer starts you ask, but you did not really say you can not, and he says something to you stayed after school.'
				'Classes are over, but you have to stay.'

				act 'Remain in the classroom':
					cla
					*clr
					zanpicrand = rand(1, 3)
					'<center><img src="images/img/oldtown/zan<<zanpicrand>>.jpg"></center>'
					'After the class lecturer explains to you for an hour thing.'

					act 'Leave':gt 'univer', 'dekanat'

					act 'Seduce':
						cla
						*clr

						if zanpicrand = 1:picrand = 44
						if zanpicrand = 2:picrand = 45
						if zanpicrand = 3:picrand = 46

						'<center><img src="images/img/oldtown/zan1<<zanpicrand>>.jpg"></center>'
						'You grab his penis, he reflexively pulls away, but then relaxes and allows you to continue.'

						act 'Blow job':gt 'sex', 'minet'
					end
				end
			elseif zanrand = 1:
				minut += 60
				'During class lecturer starts you ask, but you did not really say you can not, and he says something to you stayed after school.'
				'Classes are over, but you have to stay.'

				act 'Remain in the classroom':
					cla
					*clr
					zanpicrand = rand(4, 6)
					'<center><img src="images/img/oldtown/zan<<zanpicrand>>.jpg"></center>'
					'After exercising for an hour lecturer explains to you another girl the importance of its subject.'

					act 'Leave':gt 'univer', 'dekanat'

					act 'Seduce':
						cla
						*clr

						if zanpicrand = 4:picrand = 20
						if zanpicrand = 5:picrand = 21
						if zanpicrand = 6:picrand = 22

						'<center><img src="images/img/oldtown/zan1<<zanpicrand>>.jpg"></center>'
						'You go down on your knees and grab his penis, the other girl looks at you with round eyes, but then also starts to stick to the teacher to design, he resists a little at first, but then completely given into your hand.'

						act 'Blow job':gt 'podrsex', 'var'
					end
				end
			end

			act 'Exit from the audience':gt 'univer', 'dekanat'
		end
	end

	if kursovik >= 100:
		act 'Pass the course work':
			cla
			*clr
			intel += 1
			kursovik = 0
			kursovikD += 1
			'<center><img src="images/etogame/Classroom1.jpg"></center>'
			'You passed the course work.'

			act 'Exit from the audience':gt 'univer', 'dekanat'
		end
	end
end

if $ARGS[0] = 'examen':
	cla
	clr
	*clr
	minut += 60
	gs 'stat'
	'<center><b><font color="maroon">Exam</font></b></center>'
	'<center><img src="images/etogame/examen.jpg"></center>'

	if lektor > 55 and kursovikD >= kurs:
		examen = 1
		kursovikD = 0
		stipuha = 1
		lektor = 0
		!!1200
		'You are well attended classes and you have no outstanding coursework, exam so you put a gun.'
		jump 'exitexamem'
	end

	if ((intel >= 80) or (lektor > 40 and lektor <= 55)) and kursovikD >= kurs:
		examen = 1
		kursovikD = 0
		stipuha = 0
		lektor = 0
		'You answered all the questions in the ticket and you have no outstanding coursework, so you score? Exam.'

		act 'Exit from the audience':gt 'univer', 'dekanat'

		exit
	end

	if (intel >= 100) and kursovikD >= kurs + 10:
		examen = 1
		kursovikD = 0
		stipuha = 1
		lektor = 0
		'You showed great zeal and independence in learning, so you put a gun exam.'
		jump 'exitexamem'
		!!exit
	end

	if (intel < 70 and lektor <= 40) or kursovikD < kurs:
		examen = 2
		stipuha = 0
		'You do not pass the exam.'
	end

	if examen = 0:
		examen = 2
		stipuha = 0
		'You do not pass the exam.'
	end

	:exitexamem

	act 'Exit from the audience':gt 'univer', 'dekanat'
end

--- univer ---------------------------------
