# stwork
if $ARGS[0] = 'start':
	$stripgirl = {
		cls
		gs 'stat'
		striptalkrand = rand(0, 10)

		if striptalkrand = 0:$striptalk = 'Have you heard? Then came one somehow the girl, all so beautiful, but the stage was full of zero can not do anything, so it only booed.'
		if striptalkrand = 1:$striptalk = 'I''m telling you, men give birth to girls with tattoos.'
		if striptalkrand = 2:$striptalk = 'On a pole over much more profitable than just dance striptease, easier to excite the customer, but this pole is difficult.'
		if striptalkrand = 3:$striptalk = 'Phew, it does not go out on stage, going back all sweaty, disheveled hair and makeup certainly splyvet. Here one can go bankrupt cosmetics.'
		if striptalkrand = 4:$striptalk = 'Generally these private dances, though guaranteed money but it is not on the scene can make more money.'
		if striptalkrand = 5:$striptalk = 'Then the girl was ugly one, came on the scene, immediately let her boo, but she started spinning around a pole, customers jaw dropped, it is not bad earned.'
		if striptalkrand = 6:$striptalk = 'And why the manager never says male or female day? I recently rushed to work, even nail broke, I take refuge, and here these bears jump on stage. Turned around and went back.'
		if striptalkrand = 7:$striptalk = 'Some dechonki told that they had problems arise on the job. For many stritizersha power whore.'
		if striptalkrand = 8:$striptalk = 'Here one fat woman trying to dance on the pole, so muzzle flopped on the floor and put a strong bruise.'
		if striptalkrand = 9:$striptalk = 'Recently one zhirnuhu raised a laugh in the hall. Like and simpotnaya and dances well, but to no avail.'
		if striptalkrand = 10:$striptalk = 'I heard a decent clubs abroad strippers themselves pay for the right to work in the club.'

		'Girls are engaged with their appearance in the mirror fun chatting about all sorts of trivia.'
		'<<$striptalk>>'

		act 'Move away':gt $curloc, 'start'
	}

	$stripohr = {
		cls
		gs 'stat'
		'Hefty bored guard at the door. Seeing you it lazily throws "Hello <<$name>>."'

		act 'Move away':gt $curloc, 'start'
	}

	$striptoilet2 = {
		cls
		gs 'stat'
		'You rinsed the razor blade which will be enough for <<stanok>> time.'

		if leghair > 0 and stanok > 0:
			act 'Shave your legs (0:15)':
				cla
				*clr
				minut += 15
				leghair = -5
				horny += 5
				stanok -= 1
				'<center><img src="images/pics/shave.jpg"></center>'
				'You lathered up and shaved hair with them.'

				act 'Postpone machine':dynamic $striptoilet2
			end
		end

		if lobok > 0 and stanok > 0:
			act 'Shave pubis (0:15)':
				cla
				*clr
				minut += 15
				lobok = -5
				horny += 5
				stanok -= 1
				'<center><img src="images/pics/shave3.jpg"></center>'
				'You lathered pubis, then shaved all the hair from it.'

				act 'Postpone machine':dynamic $striptoilet2
			end
		end

		act 'Out of the shower and get dressed':dynamic $striptoilet
	}

	$striptoilet = {
		cls
		gs 'stat'
		'<center><img src="images/LakeCafe/Toilet.jpg"></center>'
		'Dirty and smelly toilet that seems to have never been cleaned'

		if tanga = 1:
			act 'Remove Underwear':
				cla
				tanga = 0
				dynamic $striptoilet
			end
		end

		act 'Look in the mirror':
			cla
			gs 'portmirror'
			$gt1 = 'stwork'
			$gt2 = ''
			dynamic $portmirror

			act 'Move away from the mirror':
				cla
				dynamic $striptoilet
			end
		end

		if mop ! 1:
			act 'Wash off makeup':
				cla
				*clr
				minut += 5
				mop = 1
				'<center><img src="images/etogame/mop.jpg"></center>'
				'You flushed makeup.'

				act 'Leave':dynamic $striptoilet
			end
		end

		act 'Take a shower (0:15)':
			cla
			*clr
			minut += 15
			sweat = -3
			horny += 1
			hapri = 0
			mop = 1
			cumpussy = 0
			cumbelly = 0
			cumass = 0
			cumanus = 0
			cumlip = 0
			cumface = 0
			cumanus =0
			'<center><img src="images/pics/dush.jpg"></center>'
			'You got into the bathroom and turned on the shower. Lathered body and washed off in the shower.'

			if leghair > 0 and stanok > 0 or lobok > 0 and stanok > 0:
				act 'Get razor':dynamic $striptoilet2
			end

			if horny >= 90:
				act 'Send shower pussy':
					cla
					*clr
					horny = 0
					minut += 30
					mastr += 1
					orgasm += 1
					manna += 15
					willpower += 15
					'<center><img src="images/pics/wash2.jpg"></center>'
					'You sent a shower pussy and tight jet of warm water began to caress you. Soon you will feel like a warm wave covers your entire body and you began to shudder in orgasm.'

					act 'Get out and get dressed':dynamic $striptoilet
				end
			elseif horny >= 40 and hormy < 90:
				act 'Send shower pussy':
					cla
					*clr
					horny += 5
					minut += 30
					manna += 10
					willpower += 10
					'<center><img src="images/pics/wash2.jpg"></center>'
					'You sent a shower pussy and tight jet of warm water began to caress you. First you were very nice, but after a while, even as it was painful. Excitation of sleep and you stop masturbating.'

					act 'Get out and get dressed':dynamic $striptoilet
				end
			end

			act 'Out of the shower and get dressed':dynamic $striptoilet
		end

		!!0.2.6 extension: reworked how quick wash and face wash was supposed to work, it is now uniform in all toilet/bathroom locations
		!!Removed sweat =- 1 when cum is wiped off clothes
		!!clrbelo: Amount of clean panties available
		if cumpussy > 0 or cumbelly > 0 or cumass > 0 or cumanus > 0:
			act 'Quick wash (0:15)':
				cla
				*clr

				if clrbelo > 0 and tanga = 1:
					clrbelo -= 1
					dirtbelo += 1
				elseif clrbelo = 0 and tanga = 1:
					tanga = 0
					dirtbelo += 1
				end

				dynamic $showerdin
				sweat -= 1
				minut += 15
				'<center><img src="images/pics/wash5.jpg"></center>'
				'You gave yourself a catlick, thus removing any traces of cum.'

				act 'Finish':dynamic $striptoilet
			end
		end

		if cumlip > 0 or cumface > 0:
			act 'Wash cum off face (0:15)':
				cla
				*clr
				mop = 1
				cumlip = 0
				cumface = 0
				sweat -= 1
				minut += 15
				'<center><img src="images/etogame/facesp.jpg"></center>'
				'You clean your face and got rid of any trace of cum.'
				act 'Finish':dynamic $striptoilet
			end
		end

		if cumfrot > 0:
			act 'Wipe the cum off your clothes (0:10)':
				cla
				*clr
				cumfrot = 0
				minut += 10
				!!'<center><img src="images/etogame/frotsp.jpg"></center>'
				'You remove traces of cum from the clothes.'

				act 'Finish':dynamic $striptoilet
			end
		end
		!!

		act 'Off the toilet':gt $curloc, 'start'
	}
	$sexloc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	clr
	minut += 5
	stripMir = 1
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Dressing for strippers</font></b></center>'
	'<center><img src="images/etogame/striprazd.jpg"></center>'
	'On the wall of the <a href="exec:gt ''mirror'', ''start''">mirrors</a> to whom <a href="exec:dynamic $stripgirl">girls</a> lead myself up before performances.'
	'<a href="exec:stripMir = 0 & gt ''stripclub'', ''start''">Do not noticeable, steel door</a> in the hallway overlooks the back yard and all the staff goes through the door to work, and not through the front door. About this entry is always worth <a href="exec:dynamic $stripohr">guard</a>.'
	'Behind wooden door located <a href="exec:dynamic $striptoilet">toilet for staff</a>'
	if tatplech > 0:stripKoef = rand(1, 3)
	if tatback > 0:stripKoef2 = rand(1, 3)
	if tatback > 0:stripKoef3 = rand(1, 3)
	if tatupb > 0:stripKoef4 = rand(3, 6)
	if tatleg > 0:stripKoef5 = rand(1, 3)

	publichot = vnesh - 10 + stripKoef + stripKoef2 + stripKoef3 + stripKoef4 + stripKoef5
	publichotstart = publichot

	if hour >= 15 or hour <= 2:
		if mop >= 4:
			if hapri = 1:
				if sweat <= 0:
					if leghair <= 0 and lobok <= 0:
						if fingal <= 0:
							act 'Go on stage':stpredmet = 3 & gt 'stwork2'
						else
							'With such a black eye as you have nothing to do on stage.'
						end
					else
						'There is nothing more terrible than a stripper with hairy legs or pubis, in this form can not work.'
					end
				else
					'Lord, from you the same stinks like a horse, one must first wash.'
				end
			else
				'Not with this disheveled hair pokazyvatsya in public.'
			end
		else
			'Need to work very defiant makeup.'
		end
	else
		'Working time is over. Time to go home.'
	end
end

--- stwork ---------------------------------
